/* Creaci√≥n de procedimientos para la carga desde las tablas INT a las 
tablas finales de dimensiones y Fact */

USE DW_COMERCIAL
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


-- SPs para carga DIMS

CREATE PROCEDURE sp_carga_dim_producto
AS
BEGIN
	SET NOCOUNT ON;

	-- ACTUALIZO REGISTROS EXISTENTES
	UPDATE d
	SET d.DESC_PRODUCTO = i.DESC_PRODUCTO
	FROM DIM_PRODUCTO AS d
		INNER JOIN INT_DIM_PRODUCTO AS i
			ON d.COD_PRODUCTO = i.COD_PRODUCTO

	-- INSERTO NUEVOS REGISTROS
	INSERT INTO DIM_PRODUCTO
	(
		COD_PRODUCTO,
		DESC_PRODUCTO
	)
	SELECT 
		i.COD_PRODUCTO,
		i.DESC_PRODUCTO
	FROM INT_DIM_PRODUCTO AS i
		LEFT JOIN DIM_PRODUCTO AS d
			ON i.COD_PRODUCTO = d.COD_PRODUCTO
	WHERE d.COD_PRODUCTO IS NULL
END
GO


CREATE PROCEDURE sp_carga_dim_categoria
AS
BEGIN
	SET NOCOUNT ON;

	-- ACTUALIZO REGISTROS EXISTENTES
	UPDATE d
	SET d.DESC_CATEGORIA = i.DESC_CATEGORIA
	FROM DIM_CATEGORIA AS d
		INNER JOIN INT_DIM_CATEGORIA AS i
			ON d.COD_CATEGORIA = i.COD_CATEGORIA

	-- INSERTO NUEVOS REGISTROS
	INSERT INTO DIM_CATEGORIA
	(
		COD_CATEGORIA,
		DESC_CATEGORIA
	)
	SELECT 
		i.COD_CATEGORIA,
		i.DESC_CATEGORIA
	FROM INT_DIM_CATEGORIA AS i
		LEFT JOIN DIM_CATEGORIA AS d
			ON i.COD_CATEGORIA = d.COD_CATEGORIA
	WHERE d.COD_CATEGORIA IS NULL
END
GO


CREATE PROCEDURE sp_carga_dim_pais
AS
BEGIN
	SET NOCOUNT ON;

	-- ACTUALIZO REGISTROS EXISTENTES
	UPDATE d
	SET d.DESC_PAIS = i.DESC_PAIS
	FROM DIM_PAIS AS d
		INNER JOIN INT_DIM_PAIS AS i
			ON d.COD_PAIS = i.COD_PAIS

	-- INSERTO NUEVOS REGISTROS
	INSERT INTO DIM_PAIS
	(
		COD_PAIS,
		DESC_PAIS
	)
	SELECT 
		i.COD_PAIS,
		i.DESC_PAIS
	FROM INT_DIM_PAIS AS i
		LEFT JOIN DIM_PAIS AS d
			ON i.COD_PAIS = d.COD_PAIS
	WHERE d.COD_PAIS IS NULL
END
GO


CREATE PROCEDURE sp_carga_dim_sucursal
AS
BEGIN
	SET NOCOUNT ON;

	-- ACTUALIZO REGISTROS EXISTENTES
	UPDATE d
	SET d.DESC_SUCURSAL = i.DESC_SUCURSAL
	FROM DIM_SUCURSAL AS d
		INNER JOIN INT_DIM_SUCURSAL AS i
			ON d.COD_SUCURSAL = i.COD_SUCURSAL

	-- INSERTO NUEVOS REGISTROS
	INSERT INTO DIM_SUCURSAL
	(
		COD_SUCURSAL,
		DESC_SUCURSAL
	)
	SELECT 
		i.COD_SUCURSAL,
		i.DESC_SUCURSAL
	FROM INT_DIM_SUCURSAL AS i
		LEFT JOIN DIM_SUCURSAL AS d
			ON i.COD_SUCURSAL = d.COD_SUCURSAL
	WHERE d.COD_SUCURSAL IS NULL
END
GO


CREATE PROCEDURE sp_carga_dim_cliente
AS
BEGIN
	SET NOCOUNT ON;

	-- ACTUALIZO REGISTROS EXISTENTES
	UPDATE d
	SET d.NOMBRE = i.NOMBRE,
		d.APELLIDO = i.APELLIDO	
	FROM DIM_CLIENTE AS d
		INNER JOIN INT_DIM_CLIENTE AS i
			ON d.COD_CLIENTE = i.COD_CLIENTE

	-- INSERTO NUEVOS REGISTROS
	INSERT INTO DIM_CLIENTE
	(
		COD_CLIENTE,
		NOMBRE,
		APELLIDO
	)
	SELECT 
		i.COD_CLIENTE,
		i.NOMBRE,
		i.APELLIDO
	FROM INT_DIM_CLIENTE AS i
		LEFT JOIN DIM_CLIENTE AS d
			ON i.COD_CLIENTE = d.COD_CLIENTE
	WHERE d.COD_CLIENTE IS NULL
END
GO


CREATE PROCEDURE sp_carga_dim_vendedor
AS
BEGIN
	SET NOCOUNT ON;

	-- ACTUALIZO REGISTROS EXISTENTES
	UPDATE d
	SET d.NOMBRE = i.NOMBRE,
		d.APELLIDO = i.APELLIDO	
	FROM DIM_VENDEDOR AS d
		INNER JOIN INT_DIM_VENDEDOR AS i
			ON d.COD_VENDEDOR = i.COD_VENDEDOR

	-- INSERTO NUEVOS REGISTROS
	INSERT INTO DIM_VENDEDOR
	(
		COD_VENDEDOR,
		NOMBRE,
		APELLIDO
	)
	SELECT 
		i.COD_VENDEDOR,
		i.NOMBRE,
		i.APELLIDO
	FROM INT_DIM_VENDEDOR AS i
		LEFT JOIN DIM_VENDEDOR AS d
			ON i.COD_VENDEDOR = d.COD_VENDEDOR
	WHERE d.COD_VENDEDOR IS NULL
END
GO




-- SP para carga FACT
CREATE PROCEDURE sp_carga_fact_ventas
@FechaDesde SMALLDATETIME,
@FechaHasta SMALLDATETIME

AS
BEGIN
	SET NOCOUNT ON;

	DELETE FROM FACT_VENTAS
	WHERE TIEMPO_KEY BETWEEN @FechaDesde AND @FechaHasta

	-- INSERTO NUEVOS REGISTROS
	INSERT INTO FACT_VENTAS
	(
		[PRODUCTO_KEY]
		,[CATEGORIA_KEY]
		,[CLIENTE_KEY]
		,[PAIS_KEY]
		,[VENDEDOR_KEY]
		,[SUCURSAL_KEY]
		,[TIEMPO_KEY]
		,[CANTIDAD_VENDIDA]
		,[MONTO_VENDIDO]
		,[PRECIO]
		,[COMISION_COMERCIAL]
	)
	SELECT 
		ISNULL(prod.PRODUCTO_KEY,-1) AS PRODUCTO_KEY, 
		ISNULL(cat.CATEGORIA_KEY,-1) AS CATEGORIA_KEY, 
		ISNULL(cli.CLIENTE_KEY,-1) AS CLIENTE_KEY, 
		ISNULL(pa.PAIS_KEY,-1) AS PAIS_KEY, 
		ISNULL(vend.VENDEDOR_KEY,-1) AS VENDEDOR_KEY, 
		ISNULL(suc.SUCURSAL_KEY,-1) AS SUCURSAL_KEY, 
		ISNULL(t.TIEMPO_KEY,
			CAST('1900-01-01 00:00:00' AS SMALLDATETIME) 
			) AS TIEMPO_KEY,
		i.CANTIDAD_VENDIDA,
		i.MONTO_VENDIDO,
		i.PRECIO,
		i.COMISION_COMERCIAL
	FROM INT_FACT_VENTAS AS i
		LEFT JOIN DIM_PRODUCTO AS prod 
			ON i.COD_PRODUCTO = prod.COD_PRODUCTO
		LEFT JOIN DIM_CATEGORIA AS cat 
			ON i.COD_CATEGORIA = cat.COD_CATEGORIA
		LEFT JOIN DIM_CLIENTE AS cli 
			ON i.COD_CLIENTE = cli.COD_CLIENTE
		LEFT JOIN DIM_PAIS AS pa
			ON i.COD_PAIS = pa.COD_PAIS
		LEFT JOIN DIM_SUCURSAL AS suc
			ON i.COD_SUCURSAL = suc.COD_SUCURSAL
		LEFT JOIN DIM_VENDEDOR AS vend 
			ON i.COD_VENDEDOR = vend.COD_VENDEDOR
		LEFT JOIN DIM_TIEMPO AS t
			ON i.Fecha = t.TIEMPO_KEY 
	WHERE i.Fecha BETWEEN @FechaDesde AND @FechaHasta
END

GO

-- Carga de tablas de dimension
EXECUTE sp_carga_dim_producto;
EXECUTE sp_carga_dim_categoria;
EXECUTE sp_carga_dim_pais;
EXECUTE sp_carga_dim_sucursal;
EXECUTE sp_carga_dim_cliente;
EXECUTE sp_carga_dim_vendedor;

-- Control
SELECT * FROM DIM_PRODUCTO;
SELECT * FROM DIM_CATEGORIA;
SELECT * FROM DIM_PAIS;
SELECT * FROM DIM_SUCURSAL;
SELECT * FROM DIM_CLIENTE;
SELECT * FROM DIM_VENDEDOR;

GO

-- Carga de tabla de hechos
-- Encuentro fecha minima y fecha maxima
DECLARE @fecha_minima AS SMALLDATETIME;
DECLARE @fecha_maxima AS SMALLDATETIME;
SELECT @fecha_minima = MIN(Fecha), -- 2016
	@fecha_maxima = MAX(Fecha)	 -- 2021 
FROM INT_FACT_VENTAS;
-- Ejecuto el procedimiento con los valores encontrados
EXECUTE sp_carga_fact_ventas @fecha_minima, @fecha_maxima;


-- Control
SELECT TOP 100 * FROM FACT_VENTAS 